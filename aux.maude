mod AUX is
    protecting SYNTAX .
    protecting NAT .
    protecting FLOAT .

    vars T T1 T2 : Term .
    vars TA1 TA2 OA : OrderedArgs .
    vars UA1 UA2 UA : UnorderedArgs .
    vars RP RP2 : RelationPair .
    var RS : RelationSet .
    var F : Float .
    var S : String . 
    var V : VarSym .
    var E : equation .
    var C : CNF .
    var S1 : FunSym .
    
    op getVal : RelationSet RelationPair -> Float .
    eq getVal(RS, R(S1, S1)) = 1.0 .
    eq getVal((RP = F) && RS, RP) = F .
    eq getVal(RS, RP) = 0.0 [owise] .


    eq # nil = 0 .
    eq # (T , TA1) = 1 + (# TA1) .

    eq # mnil = 0 .
    eq # (T ; UA1) = 1 + (# UA1) .

    op zip : OrderedArgs OrderedArgs -> CNF .
    eq zip(nil, nil) = Cnil .
    eq zip((T1, TA1), (T2, TA2)) = (T1 ~ T2) /\ zip(TA1, TA2) .

    op unzip : UnorderedArgs UnorderedArgs -> CNF .
    rl [step] : unzip((T1 ; UA1), (T2 ; UA2)) => (T1 ~ T2) /\ unzip(UA1, UA2) .
    rl [success] : unzip(mnil, mnil) => Cnil .
    crl [fail] : unzip(UA1, UA2) => Cnil if # UA1 =/= # UA2 .

    op getSym : Term -> FunSym .
    eq getSym(Urd(S){UA1}) = Urd(S) .
    eq getSym(Ord(S)(TA1)) = Ord(S) .


    eq V ∈ Var(S) = (Var(S) == V) .
    eq V ∈ Ord(S)(TA1) = V ∈ TA1 .
    eq V ∈ Urd(S){UA1} = V ∈ UA1 .
    eq V ∈ T:Term = false [owise] .

    eq V ∈ nil = false .
    eq V ∈ (T, TA1) = V ∈ T or V ∈ TA1 .

    eq V ∈ mnil = false .
    eq V ∈ (T ; UA1) = V ∈ T or V ∈ UA1 .

    op applySubst : CNF VarSym Term -> CNF .
    op applySubstEq : equation VarSym Term -> equation .
    op applySubstTerm : Term VarSym Term -> Term .
    op applySubstOrd : OrderedArgs VarSym Term -> OrderedArgs .
    op applySubstUrd : UnorderedArgs VarSym Term -> UnorderedArgs .

    eq applySubst(E /\ C, V, T) = applySubstEq(E, V, T) /\ applySubst(C, V, T) .
    eq applySubst(Cnil, V, T) = Cnil .
    
    eq applySubstEq(T1 ~ T2, V, T) = applySubstTerm(T1, V, T) ~ applySubstTerm(T2, V, T) .

    eq applySubstTerm(V, V, T) = T . 
    eq applySubstTerm(Ord(S)(OA), V, T) = Ord(S)(applySubstOrd(OA, V, T)) .
    eq applySubstTerm(Urd(S){UA}, V, T) = Urd(S){applySubstUrd(UA, V, T)} .
    eq applySubstTerm(T1, V, T) = T1 [owise] .

    eq applySubstOrd((T1, OA), V, T) = (applySubstTerm(T1, V, T), applySubstOrd(OA, V, T)) .
    eq applySubstOrd(nil, V, T) = nil .

    eq applySubstUrd((T1 ; UA), V, T) = (applySubstTerm(T1, V, T) ; applySubstUrd(UA, V, T)) .
    eq applySubstUrd(mnil, V, T) = mnil .
endm