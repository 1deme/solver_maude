mod SOLVER is
    protecting SYNTAX .
    protecting AUX .

    vars S1 S2 S : String .
    vars T T1 T2 : Term .
    vars TA1 TA2 : OrderedArgs .
    vars UA1 UA2 : UnorderedArgs .
    var K Sigma : CNF .
    var RS : RelationSet .
    vars F1 F2 RVal : Float .


    rl [Var-Del-Sim] : 
        < (Var(S) ~ Var(S)) /\ K | Sigma ; F1 >(RS) F2 => < K | Sigma ; F1 >(RS) F2 .

    crl [Ord-Dec-Sim] :
        < (Ord(S1)(TA1) ~ Ord(S2)(TA2)) /\ K | Sigma ; F1 >(RS) F2 => 
            < zip(TA1, TA2) /\ K | Sigma ; min(F1, RVal) >(RS) F2
        if RVal := RS[R(Ord(S1), Ord(S2))] /\ RVal >= F2 /\ # TA1 == # TA2 .

    crl [Urd-Dec-Sim] :
        < (Urd(S1){UA1} ~ Urd(S2){UA2}) /\ K | Sigma ; F1 >(RS) F2 => 
            < unzip(UA1, UA2) /\ K | Sigma ; min(F1, RVal) >(RS) F2
        if RVal := RS[R(Urd(S1), Urd(S2))] /\ RVal >= F2 /\ # UA1 == # UA2 .
    
    crl [Var-Ori-Sim] :
        < (T2 ~ Var(S)) /\ K | Sigma ; F1 >(RS) F2 => < (Var(S) ~ T2) /\ K | Sigma ; F1 >(RS) F2 
        if not (T2 :: VarSym) .

    crl [Sym-Con-Sim] :
        < (T1 ~ T2) /\ K | Sigma ; F1 >(RS) F2 => Fail
        if not (T1 :: VarSym) /\ not (T2 :: VarSym) /\ RS[R(getSym(T1), getSym(T2))] < F2 .

    crl [Occurs-Check-Sim] :
        < (Var(S) ~ T) /\ K | Sigma ; F1 >(RS) F2 => Fail
        if T =/= Var(S) /\ Var(S) ∈ T .

    crl [Sol-Con-Sim] :
        < (Var(S) ~ T) /\ K | Sigma ; F1 >(RS) F2 =>
            < applySubst(K, Var(S), T) | (Var(S) ~ T) /\ applySubst(Sigma, Var(S), T) ; F1 >(RS) F2
        if T =/= Var(S) /\ not Var(S) ∈ T .
    
endm